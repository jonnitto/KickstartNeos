prototype(Queo.Forms:Component.Atom.Form) < prototype(Neos.Fusion:Component) {

    // public api
    formId      = null
    campaignId  = null
    clientId    = null
    refererNode = null
    formDefinition = ${[]}
    uniqueHash = ''

    // internal
    formDefinition = ${Queo.Forms.fetchForm(this.formId)}
    formAction = Neos.Fusion:UriBuilder {
        package = 'Queo.Forms'
        controller = 'FormSubmission'
        action = 'submit'
    }

    uniqueHash = ${Queo.Forms.fetchUniqueHash()}
    uniqueHash.@if.onlyInFrontend = ${!node.context.inBackend}

    renderer = afx`
        <form method="post" action={props.formAction}>
            <input type="hidden" name="uniqueHash" value={props.uniqueHash} />
            <input type="hidden" name="refererNode" value={props.refererNode} />
            <input type="hidden" name="client" value={props.clientId} />
            <input type="hidden" name="campaign" value={props.campaignId} />
            <input type="hidden" name="form" value={props.formId} />
            <Queo.Forms:FieldListRenderer
                fieldList={props.formDefinition.form_fields} />
            <button class="btn btn-primary" type="submit">{props.formDefinition.submit_text}</button>
        </form>
    `
}